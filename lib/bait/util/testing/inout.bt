// Copyright (c) 2023-present Lukas Neubert and contributors (see AUTHORS.md).
// This Source Code is subject to the terms of the Mozilla Public License 2.0.
package testing

import os

pub fun in_out_file_runner(dir string, options string) {
	in_files := os.walk_ext(dir, '.in.bt')

	for file in in_files {
		mut in_path := file
		name_ext := os.file_name(file)
		name := name_ext.all_before('.in.bt')

		out_file := '${dir}/${name}.out'
		if not os.exists(out_file) {
			eprintln('out file ${out_file} does not exist')
			assert false
			continue
		}

		mut cmd := 'build'
		is_test := name_ext.contains('_test.in.bt')
		if is_test {
			cmd = 'test'
			in_path = file.replace('.in', '')
			os.cp(file, in_path)
		}

		expected := os.read_file(out_file)
		res := os.exec('node ${$BAITEXE} --nocolor ${options} ${cmd} ${in_path}')
		assert expected == res.stderr

		if is_test {
			os.rm(in_path)
		}
	}
}

pub fun in_out_dir_runner(root string, options string) {
	dirs := os.ls(root)
	for dir in dirs {
		in_dir := os.join_path(root, [dir])
		if not os.is_dir(in_dir) {
			continue
		}

		out_file := '${in_dir}/expected.out'
		if not os.exists(out_file) {
			eprintln('out file ${out_file} does not exist')
			assert false
			continue
		}

		expected := os.read_file(out_file)
		res := os.exec('node ${$BAITEXE} --nocolor ${options} run ${in_dir}')
		assert expected == res.stdout
		assert res.stderr.length == 0
	}
}
